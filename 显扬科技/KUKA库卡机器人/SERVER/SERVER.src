&ACCESS RVP
&REL 6
&PARAM DISKPATH = KRC:\R1\Program
DEF SERVER()

BAS(#INITMOV, 0) 
PTP $AXIS_ACT 

RET=EKI_Init("CONNECT")
RET=EKI_Open("CONNECT")

LOOP
   
CONTINUE
WAIT FOR ($FLAG[1] AND $OUT[14]) 
$OUT[14]=FALSE

$TOOL = tool_data[1]
$LOAD = load_data[1]
$BASE = base_data[1]

RET=EKI_GetBool("CONNECT","Robot/readRobotStatus",READROBOTSTATUS)

IF READROBOTSTATUS==TRUE THEN
    RET=EKI_SetReal("CONNECT","Robot/Pos/X",$POS_ACT.X)
    RET=EKI_SetReal("CONNECT","Robot/Pos/Y",$POS_ACT.Y)
    RET=EKI_SetReal("CONNECT","Robot/Pos/Z",$POS_ACT.Z)
    RET=EKI_SetReal("CONNECT","Robot/Pos/A",$POS_ACT.A)
    RET=EKI_SetReal("CONNECT","Robot/Pos/B",$POS_ACT.B)
    RET=EKI_SetReal("CONNECT","Robot/Pos/C",$POS_ACT.C)
    RET=EKI_SetBool("CONNECT","Robot/chuck",$OUT[2])
    RET=EKI_SetBool("CONNECT","Robot/ready",$OUT[1])
    RET=EKI_Send("CONNECT","Robot")

ELSE
    RET=EKI_GetBool("CONNECT","Robot/onlySetIO",ONLYSETIO)

    IF ONLYSETIO==TRUE THEN
    RET=EKI_GetBool("CONNECT","Robot/chuck",$OUT[2])
    RET=EKI_Send("CONNECT","Robot")

    ELSE
    RET=EKI_GetReal("CONNECT","Robot/Pos/X",POS_FR.X)
    RET=EKI_GetReal("CONNECT","Robot/Pos/Y",POS_FR.Y)
    RET=EKI_GetReal("CONNECT","Robot/Pos/Z",POS_FR.Z)
    RET=EKI_GetReal("CONNECT","Robot/Pos/A",POS_FR.A)
    RET=EKI_GetReal("CONNECT","Robot/Pos/B",POS_FR.B)
    RET=EKI_GetReal("CONNECT","Robot/Pos/C",POS_FR.C)

    $VEL.CP=0.9
    $VEL.ORI1=5
    $VEL.ORI2=5

    $APO.CVEL=30
    $APO.CDIS=20
    LIN POS_FR C_VEL


    RET=EKI_Send("CONNECT","Robot")
    ENDIF

ENDIF

ENDLOOP

RET=EKI_Close("CONNECT")
RET=EKI_Clear("CONNECT")

END